plugins {
    id 'org.hidetake.swagger.generator' version '2.18.2'
    id 'java'
}

repositories {
    jcenter()
}

dependencies {
    implementation project(":boggle-lib")
    swaggerCodegen 'org.openapitools:openapi-generator-cli:4.3.1'     // or OpenAPI Generator
    implementation group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.0'
    implementation group: 'com.sun.xml.bind', name: 'jaxb-core', version: '2.3.0'
    implementation group: 'com.sun.xml.bind', name: 'jaxb-impl', version: '2.3.0'
    implementation group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'
    implementation "org.springframework.boot:spring-boot-starter-web:2.1.+"
    implementation "org.springframework.boot:spring-boot-starter-tomcat:2.1.+"
    implementation "io.springfox:springfox-swagger2:2.8.0"
    implementation "io.springfox:springfox-swagger-ui:2.8.0"
    implementation "com.github.joschi.jackson:jackson-datatype-threetenbp:2.6.4"
    implementation "javax.validation:validation-api"
    implementation group: 'org.openapitools', name: 'jackson-databind-nullable', version: '0.1.0'
    testImplementation "org.springframework.boot:spring-boot-starter-test:2.1.+"
}

swaggerSources {
    boggle {
        inputFile = file('../../boggle.yaml')
        code {
            language = 'spring'
            rawOptions = ["--ignore-file-override=" + file(".swagger-codegen-ignore").absolutePath]
        }
    }
    boggleClient {
        inputFile = file('../../boggle.yaml')
        code {
            language = 'typescript-fetch'
            outputDir = file('../../boggle-frontend/src/swagger/')
            additionalProperties = [
                typescriptThreePlus: true,
                supportsES6: true
            ]
        }
    }
}

tasks.register("writeApplicationDotProperties") {
    doLast {
        def props = project.file("build/resources/main/application.properties");
        props.delete();
        props << """server.port=8080
spring.jackson.serialization.WRITE_DATES_AS_TIMESTAMPS=false
spring.servlet.multipart.max-file-size=50MB
spring.servlet.multipart.max-request-size=50MB
"""
    }
}

compileJava.dependsOn swaggerSources.boggle.code
compileJava.dependsOn tasks.getByName("writeApplicationDotProperties")
tasks.getByName("writeApplicationDotProperties").shouldRunAfter processResources
compileJava.dependsOn swaggerSources.boggleClient.code
sourceSets.main.java.srcDir "${swaggerSources.boggle.code.outputDir}/src/main/java"
sourceSets.main.resources.srcDir "${swaggerSources.boggle.code.outputDir}/src/main/resources"