plugins {
    id 'org.hidetake.swagger.generator' version '2.18.2'
    id 'java'
}

repositories {
    jcenter()
}

dependencies {
    implementation project(":boggle-lib")
    swaggerCodegen 'io.swagger:swagger-codegen-cli:2.4.2'             // Swagger Codegen V2
    swaggerCodegen 'io.swagger.codegen.v3:swagger-codegen-cli:3.0.5'  // or Swagger Codegen V3
    swaggerCodegen 'org.openapitools:openapi-generator-cli:3.3.4'     // or OpenAPI Generator
    implementation group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.0'
    implementation group: 'com.sun.xml.bind', name: 'jaxb-core', version: '2.3.0'
    implementation group: 'com.sun.xml.bind', name: 'jaxb-impl', version: '2.3.0'
    implementation group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'
    implementation "org.springframework.boot:spring-boot-starter-web:2.1.+"
    implementation "org.springframework.boot:spring-boot-starter-tomcat:2.1.+"
    implementation "io.springfox:springfox-swagger2:2.7.0"
    implementation "io.springfox:springfox-swagger-ui:2.7.0"
    implementation "com.github.joschi.jackson:jackson-datatype-threetenbp:2.6.4"
    implementation "javax.validation:validation-api"
    testImplementation "org.springframework.boot:spring-boot-starter-test:2.1.+"
}

swaggerSources {
    boggle {
        inputFile = file('../../boggle.yaml')
        code {
            language = 'spring'
            dependsOn(validation)
            rawOptions = ["--ignore-file-override=" + file(".swagger-codegen-ignore").absolutePath]
        }
    }
}

tasks.register("writeApplicationDotProperties") {
    doLast {
        def props = project.file("build/resources/main/application.properties");
        props.delete();
        props << """server.port=8080
spring.jackson.date-format=io.swagger.RFC3339DateFormat
spring.jackson.serialization.WRITE_DATES_AS_TIMESTAMPS=false
spring.servlet.multipart.max-file-size=50MB
spring.servlet.multipart.max-request-size=50MB
"""
    }
}

compileJava.dependsOn swaggerSources.boggle.code
compileJava.dependsOn tasks.getByName("writeApplicationDotProperties")
tasks.getByName("writeApplicationDotProperties").shouldRunAfter processResources
sourceSets.main.java.srcDir "${swaggerSources.boggle.code.outputDir}/src/main/java"
sourceSets.main.resources.srcDir "${swaggerSources.boggle.code.outputDir}/src/main/resources"